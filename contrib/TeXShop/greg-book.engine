#!/bin/tcsh

echo "Gregorio Engine for processing documents with included scores."
echo "(C) 2014 R. Padraic Springuel. This work is licensed under a Creative Commons Attribution 4.0 International License."

set path= ($path /usr/texbin /usr/local/bin)
set filename = "$1"
# scan the file for lines which add a gregorio score:
set greglines = `grep '[\]\(gre\)\?include\(tex\)\?score[{].*[}]' "$filename"`
foreach line ($greglines)
	# set the filename of the score and the included file
	set file = `expr "$line" : '.*score\(.*\)'`
    echo "Processing $file"
    # if the included file is in gabc format, then gregoriotex knows what to do with it and we can skip it here
    if ("${file:e}" == "gabc") then
        echo "Skipping $file.  gregoriotex will process later."
        continue
    endif
	set gregname = "${file:r}.gabc"
	if ("${file:r}" == "$file") then
		set output = "$file.tex"
	else
		set output = "$file"
	endif
	# check to see if the score exists
	if (! -e "$gregname") then
		# check to see if the included file exists
		if (! -e "$output") then
			echo "Error: Neither $gregname nor $output exists!"
			echo "Enter [y] to abort (else continue)."
			set prompt = $< 
			if ("$prompt" == "y") then
                echo "User aborted."
				exit
			endif
		else
			echo "Warning: Cannot update $output.  $gregname does not exist."
		endif
	else
		# check to see if the score has changed since the included file was generated
		if (-M "$gregname" > -M "$output") then
			# update the included file
			gregorio -vW  "$gregname" -o "$output"
			echo "Produced $output"
		else
			echo "$gregname has not changed since last compilation"
		endif
	endif
end
# compile the main project file
lualatex --shell-escape -file-line-error -synctex=1 "$1"
